plugins {
	id 'maven-publish'
	id 'signing'
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	title = 'ChatPlugin ' + rootProject.version + ' API'
	options.docTitle = title
	options.windowTitle = title
	options.header = title
	options.encoding = 'UTF-8'
	options.charSet = 'UTF-8'
	options.links 'https://docs.oracle.com/javase/8/docs/api'
	options.addStringOption 'Xdoclint:all', '-public'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = rootProject.group
			artifactId = 'api'
			version = rootProject.version
			from components.java
			pom {
				name = 'ChatPlugin'
				description = 'A complete yet lightweight plugin which handles just too many features!'
				url = 'https://remigio07.me/chatplugin'
				licenses {
					license {
						name = 'AGPL-3.0'
						url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
					}
				}
				developers {
					developer {
						id = 'remigio07'
						name = 'Remigio07'
						email = 'remigio07@outlook.it'
						url = 'https://remigio07.me'
					}
				}
				contributors {
					contributor {
						name = 'Keresael'
					}
				}
				scm {
					connection = 'scm:git:git:https://github.com/ChatPlugin/ChatPlugin.git'
					developerConnection = 'scm:git:ssh:https://github.com/ChatPlugin/ChatPlugin.git'
					url = 'https://github.com/ChatPlugin/ChatPlugin/tree/main'
				}
			}
		}
		
	}
	repositories {
		mavenCentral()
	}
}

signing {
	sign publishing.publications.mavenJava
}
