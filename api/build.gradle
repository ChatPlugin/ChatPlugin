plugins {
	id 'io.github.crimix.replace-placeholders' version '3.0'
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	title = 'ChatPlugin ' + rootProject.version + ' API'
	options.docTitle title
	options.windowTitle title
	options.header title
	options.encoding 'UTF-8'
	options.charSet 'UTF-8'
	options.addStringOption 'Xdoclint:all,-missing', '-public'
}

replaceSourcePlaceholders {
	version = rootProject.version
	
	enabled true
	filesToExpand 'me/remigio07/chatplugin/api/ChatPlugin.java'
	extraProperties 'version'
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
	apply plugin: 'maven-publish'
	apply plugin: 'signing'
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = rootProject.group
				artifactId = 'api'
				version = rootProject.version
				from components.java
				pom {
					name = 'ChatPlugin'
					description = 'A feature-rich and modular chat ecosystem, lightweight and efficient by design.'
					url = 'https://remigio07.me/chatplugin'
					licenses {
						license {
							name = 'AGPL-3.0'
							url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
						}
					}
					developers {
						developer {
							id = 'Remigio07'
							name = 'Remigio07'
							email = 'remigio07@outlook.it'
							url = 'https://remigio07.me'
							timezone = 'UTC+01:00 (CET)/UTC+02:00 (CEST)'
							roles = ['Lead Developer']
						}
					}
					contributors {
						contributor {
							name = 'Keresael'
							timezone = 'UTC+01:00 (CET)/UTC+02:00 (CEST)'
							roles = ['Community Manager']
						}
					}
					scm {
						connection = 'scm:git:https://github.com/ChatPlugin/ChatPlugin.git'
						developerConnection = 'scm:git:git@github.com:ChatPlugin/ChatPlugin.git'
						url = 'https://github.com/ChatPlugin/ChatPlugin'
					}
					issueManagement {
						system = 'GitHub'
						url = 'https://github.com/ChatPlugin/ChatPlugin/issues'
					}
				}
			}
		}
		repositories {
			maven {
				name = 'Central'
				url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
				
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
	
	signing {
		sign publishing.publications.mavenJava
	}
}
