plugins {
	id 'maven-publish'
	id 'signing'
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	title = 'ChatPlugin ' + rootProject.version + ' API'
	options.docTitle = title
	options.windowTitle = title
	options.header = title
	options.encoding = 'UTF-8'
	options.charSet = 'UTF-8'
	options.links 'https://docs.oracle.com/javase/8/docs/api'
	options.addStringOption 'Xdoclint:all', '-public'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = rootProject.group
			artifactId = 'chatplugin-api'
			version = rootProject.version
			from components.java
			pom {
				name = 'ChatPlugin'
				description = 'A complete yet lightweight plugin which handles just too many features!'
				url = 'https://github.com/Remigio07/ChatPlugin'
				licenses {
					license {
						name = 'GNU Affero General Public License v3.0'
						url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
					}
				}
				developers {
					developer {
						id = 'remigio07'
						name = 'Remigio07'
						email = 'remipiova@gmail.com'
					}
				}
				scm {
					connection = 'scm:svn:https://github.com/Remigio07/ChatPlugin.git'
					developerConnection = 'scm:svn:https://github.com/Remigio07/ChatPlugin.git'
					url = 'https://github.com/Remigio07/ChatPlugin.git'
				}
			}
			pom.withXml {
				def repositoriesNode = asNode().appendNode('repositories')
				
				rootProject.repos.each { name, url ->
					def node = repositoriesNode.appendNode('repository')
					
					node.appendNode('id', name)
					node.appendNode('url', uri(url))
				}
			}
		}
		
	}
	repositories {
		mavenCentral()
	}
}

signing {
	sign publishing.publications.mavenJava
}
