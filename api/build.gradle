plugins {
	alias(libs.plugins.replacePlaceholders)
}

java {
	withJavadocJar()
	withSourcesJar()
}

javadoc {
	title = "ChatPlugin ${rootProject.version} API"
	def fabricURL = 'https://maven.fabricmc.net/docs/'
	
	options.docTitle title
	options.windowTitle title
	options.header title
	options.encoding 'UTF-8'
	options.charSet 'UTF-8'
	options.addStringOption 'Xdoclint:all,-missing', '-public'
	options.links "${fabricURL}yarn-${libs.yarn.get().version}", "${fabricURL}fabric-api-${libs.fabric.api.get().version}", "${fabricURL}fabric-loader-${libs.fabric.loader.get().version}"
}

replaceSourcePlaceholders {
	version = rootProject.version
	enabled true
	filesToExpand 'me/remigio07/chatplugin/api/ChatPlugin.java'
	extraProperties 'version'
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
	apply plugin: 'maven-publish'
	apply plugin: 'signing'
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = rootProject.group
				artifactId = 'api'
				version = rootProject.version
				from components.java
				pom {
					name = 'ChatPlugin'
					description = 'A feature-rich and modular chat ecosystem, lightweight and efficient by design.'
					url = 'https://remigio07.me/chatplugin'
					inceptionYear = '2023'
					licenses {
						license {
							name = 'AGPL-3.0-or-later'
							url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'Remigio07'
							name = 'Remigio07'
							email = 'remigio07@outlook.it'
							url = 'https://remigio07.me'
							roles = ['Lead developer', 'Support team']
							timezone = 'Europe/Rome'
						}
					}
					contributors {
						contributor {
							name = 'Keresael'
							roles = ['Community manager', 'Support team']
							timezone = 'Europe/Rome'
						}
						contributor {
							name = 'Noah Pombas'
							email = 'info@noahpombas.ch'
							url = 'https://noahpombas.ch'
							roles = ['Support team']
							timezone = 'Europe/Zurich'
						}
					}
					issueManagement {
						system = 'GitHub'
						url = 'https://github.com/ChatPlugin/ChatPlugin/issues'
					}
					scm {
						connection = 'scm:git:https://github.com/ChatPlugin/ChatPlugin.git'
						developerConnection = 'scm:git:git@github.com:ChatPlugin/ChatPlugin.git'
						url = 'https://github.com/ChatPlugin/ChatPlugin'
					}
				}
			}
		}
		
		repositories {
			maven {
				name = 'Central'
				url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
				
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
	
	signing.sign publishing.publications.mavenJava
}
