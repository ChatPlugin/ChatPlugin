plugins {
	id 'java'
	id 'eclipse'
}

group 'me.remigio07'
version '1.9.0'
jar.enabled = false
ext.repos = [
		SpigotMC: 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/',
		SpongePowered: 'https://repo.spongepowered.org/repository/maven-public/',
		PaperMC: 'https://repo.papermc.io/repository/maven-public/'
		]

allprojects {
	compileTestJava.options.encoding = 'UTF-8'
	
	java {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
	
	compileJava {
		options.compilerArgs.add '-Xlint:deprecation'
		options.compilerArgs.add '-Xlint:unchecked'
		options.encoding = 'UTF-8'
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	
	repositories {
		repos.each { repoName, repoURL ->
			maven {
				name repoName
				url repoURL
			}
		}
	}
	
	dependencies {
		implementation 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
		implementation 'org.spongepowered:spongeapi:7.4.0'
		implementation 'io.github.waterfallmc:waterfall-api:1.20-R0.1-SNAPSHOT'
		implementation 'com.velocitypowered:velocity-api:3.1.1'
		compileOnly 'com.mojang:authlib:1.5.25'
	}
}
