plugins {
	id 'java'
	alias(libs.plugins.fabric.loom) apply false
}

group = 'me.remigio07.chatplugin'
version = libs.versions.chatPlugin.get()
jar.enabled = false
ext.relocation = { str -> return [str, "${group}.common.lib.${str}"] }


subprojects {
	apply plugin: 'java'
	apply plugin: 'fabric-loom'
	
	repositories {
		maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
		maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
		maven { url = 'https://repo.papermc.io/repository/maven-public' }
	}
	
	if (!name.contains("mixins")) {
		dependencies {
			compileOnly(libs.bundles.serversAPIs) { exclude group: 'org.yaml' }
			compileOnly libs.snakeYAML
			minecraft libs.minecraft
			mappings variantOf(libs.yarn) { classifier "v2" }
			modImplementation libs.bundles.fabric
		}
	}
	
	java {
		sourceCompatibility = 8
		targetCompatibility = 8
	}
	
	compileJava {
		options.compilerArgs.add '-Xlint:-options,deprecation,-missing-explicit-ctor'
		options.encoding = 'UTF-8'
	}
	
	loom.serverOnlyMinecraftJar()
	compileTestJava.options.encoding = 'UTF-8'
}
