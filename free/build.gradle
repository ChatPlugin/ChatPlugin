plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
	maven { url 'https://jitpack.io' }
	maven { url 'https://repo.viaversion.com' }
	maven { url 'https://maven.enginehub.org/repo/' }
	maven { url 'https://repo.essentialsx.net/releases/' }
	maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
	maven { url 'https://repo.rosewooddev.io/repository/public/' }
}

ext.libsDir = project.projectDir.path + '/libs/'

dependencies {
	implementation project(':api')
	
	compileOnly 'org.spongepowered:spongecommon:1.12.2-7.4.7:dev@jar'
	
	compileOnly files(libsDir + 'UltraCosmetics-3.3.jar')
	compileOnly files(libsDir + 'GadgetsMenu-4.9.3.jar')
	compileOnly files(libsDir + 'Vulcan-2.7.5.jar')
	compileOnly files(libsDir + 'Matrix-7.3.10A.jar')
	compileOnly files(libsDir + 'MVdWPlaceholderAPI-3.1.1.jar')
	compileOnly files(libsDir + 'Negativity-2.6.jar')
	compileOnly files(libsDir + 'CombatLogX-11.4.0.0.1156.jar')
	
	compileOnly 'com.github.cliftonlabs:json-simple:4.0.1'
	compileOnly 'com.viaversion:viaversion-api:4.0.0'
	compileOnly 'com.viaversion:viaversion-bukkit:4.0.0'
	compileOnly 'com.viaversion:viaversion-sponge:4.0.0'
	compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.0'
	compileOnly 'net.essentialsx:EssentialsX:2.19.0'
	compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
	compileOnly 'net.luckperms:api:5.4'
	compileOnly 'me.clip:placeholderapi:2.11.3'
	compileOnly 'dev.esophose:playerparticles:8.0'
}

jar {
	from project(':api').sourceSets.main.output
	
	exclude 'me/remigio07/chatplugin/common/**'
	exclude 'me/remigio07/chatplugin/server/**'
	exclude 'me/remigio07/chatplugin/ChatPluginImpl.class'
	exclude 'me/remigio07/chatplugin/ChatPluginImpl$1.class'
	from shadowJar.archiveFile
}

shadowJar {
	archiveFileName = 'ChatPlugin.jar'
	
	dependencies {
		include(dependency('me.remigio07.chatplugin:.*'))
	}
	
	include '**/*.class'
	
	relocate 'com.github.cliftonlabs.json_simple', 'me.remigio07.chatplugin.common.lib.com.github.cliftonlabs.json_simple'
}

processResources {
	filesMatching(['plugin.yml', 'mcmod.info', 'bungee.yml']) {
		expand 'version': rootProject.version
	}
}
