plugins {
	id 'com.gradleup.shadow' version '8.3.8'
}

def libsDir = project.projectDir.path + '/libs/'

repositories {
	maven { url = 'https://jitpack.io' }
	maven { url = 'https://repo.viaversion.com' }
	maven { url = 'https://maven.enginehub.org/repo/' }
	maven { url = 'https://repo.essentialsx.net/releases/' }
	maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
	maven { url = 'https://repo.rosewooddev.io/repository/public/' }
	maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
}

dependencies {
	compileOnly project(':api')
	compileOnly files(libsDir + 'CombatLogX-11.5.0.0.1242.jar')
	compileOnly files(libsDir + 'GadgetsMenu-4.15.3.jar')
	compileOnly files(libsDir + 'Matrix-6.8.19.jar')
	compileOnly files(libsDir + 'MVdWPlaceholderAPI-3.1.1.jar')
	compileOnly files(libsDir + 'Negativity-2.7.1.jar')
	compileOnly files(libsDir + 'UltraCosmetics-3.8.jar')
	compileOnly files(libsDir + 'Vulcan-2.9.0.jar')
	compileOnly 'org.spongepowered:spongecommon:1.12.2-7.4.7:dev@jar'
	compileOnly 'net.kyori:adventure-platform-bukkit:4.3.0'
	compileOnly 'net.kyori:adventure-platform-spongeapi:4.3.0'
	compileOnly 'com.github.cliftonlabs:json-simple:4.0.1'
	compileOnly 'com.viaversion:viaversion-api:5.2.2-SNAPSHOT'
	compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.0'
	compileOnly 'net.essentialsx:EssentialsX:2.19.0'
	compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
	compileOnly 'net.luckperms:api:5.4'
	compileOnly 'me.clip:placeholderapi:2.11.3'
	compileOnly 'dev.esophose:playerparticles:8.0'
	compileOnly 'com.discordsrv:discordsrv:1.27.0'
	implementation 'org.bstats:bstats-bukkit:3.1.0'
	implementation 'org.bstats:bstats-sponge:3.1.0'
}

task assembleFinalJAR(type: Jar) {
	archiveFileName = 'ChatPlugin-FREE-' + rootProject.version + '.jar'
	
	from project(':api').sourceSets.main.output
	from shadowJar.archiveFile
	from processResources
	manifest { attributes(['paperweight-mappings-namespace': 'mojang']) }
}

jar {
	archiveFileName = 'ChatPlugin-ORIGINAL.jar'
	
	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	finalizedBy assembleFinalJAR
}

shadowJar {
	archiveFileName = 'ChatPlugin-FREE.jar'
	
	from project.configurations.runtimeClasspath.collect { it }
	exclude(['*.yml', 'mcmod.info'])
	
	relocate 'net.kyori.adventure', 'me.remigio07.chatplugin.common.lib.net.kyori.adventure'
	relocate 'com.github.cliftonlabs.json_simple', 'me.remigio07.chatplugin.common.lib.com.github.cliftonlabs.json_simple'
	relocate 'org.bstats', 'me.remigio07.chatplugin.common.lib.org.bstats'
}

processResources {
	filesMatching(['plugin.yml', 'mcmod.info']) {
		expand 'version': rootProject.version
	}
}
