plugins {
	alias(libs.plugins.shadow)
}

def libsDir = "${projectDir}/libs/"

repositories {
	maven { url = 'https://jitpack.io' }
	maven { url = 'https://repo.viaversion.com' }
	maven { url = 'https://maven.enginehub.org/repo/' }
	maven { url = 'https://repo.essentialsx.net/releases/' }
	maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
	maven { url = 'https://repo.rosewooddev.io/repository/public/' }
	maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
}

dependencies {
	implementation project(':api')
	implementation project(':mixins')
	implementation project(':mixins:mixins-1.19')
	compileOnly files("${libsDir}CombatLogX-11.5.0.0.1242.jar")
	compileOnly files("${libsDir}GadgetsMenu-4.15.3.jar")
	compileOnly files("${libsDir}Matrix-6.8.19.jar")
	compileOnly files("${libsDir}MVdWPlaceholderAPI-3.1.1.jar")
	compileOnly files("${libsDir}Negativity-2.7.1.jar")
	compileOnly files("${libsDir}UltraCosmetics-3.8.jar")
	compileOnly files("${libsDir}Vulcan-2.9.0.jar")
	compileOnly variantOf(libs.sponge.common) { classifier 'dev' }
	compileOnly libs.jsonSimple
	compileOnly libs.discordSRV
	compileOnly(libs.essentialsX) { exclude group: 'org.yaml'; exclude group: 'org.spigotmc' } // do we need this?
	compileOnly libs.luckPermsAPI
	compileOnly libs.placeholderAPI
	compileOnly libs.playerParticles
	compileOnly(libs.vaultAPI) { exclude group: 'org.bukkit' }
	compileOnly libs.viaVersionAPI
	compileOnly(libs.worldGuardBukkit) { exclude group: 'org.bukkit' }
	implementation libs.bundles.bstats.server
}

task assembleFinalJAR(type: Jar) { // TODO: include MF in correct final jar?
	archiveFileName = "ChatPlugin-FREE-${rootProject.version}.jar"
	
	from zipTree(project(':api').remapJar.archiveFile)
	from zipTree(project(':mixins').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-latest').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.20.2').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.19.4').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.19.1').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.19').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.17').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.16.5').remapJar.archiveFile)
	from zipTree(project(':mixins:mixins-1.16').remapJar.archiveFile)
	from remapJar.archiveFile
	from processResources
}

remapJar {
	input = shadowJar.archiveFile
	archiveFileName = 'ChatPlugin-FREE.jar'
	
	finalizedBy assembleFinalJAR
}

shadowJar {
	archiveFileName = 'ChatPlugin-SHADOW.jar'
	
	dependencies {
		//include(dependency("${group}:.*"))
		include(dependency('org.bstats:.*'))
		exclude '/mappings/*'
	}
	
	exclude(['*.yml', 'mcmod.info', 'fabric.mod.json'])
	relocate(*relocation('com.github.cliftonlabs.json_simple'))
	relocate(*relocation('org.bstats'))
}

processResources {
	filesMatching(['plugin.yml', 'mcmod.info', 'fabric.mod.json']) {
		expand(
			'version': rootProject.version,
			'fabricLoaderVersion': libs.fabric.loader.get().version
		)
	}
}

