plugins {
	id 'com.gradleup.shadow' version '8.3.3'
}

repositories {
	maven { url = 'https://jitpack.io' }
	maven { url = 'https://repo.viaversion.com' }
	maven { url = 'https://maven.enginehub.org/repo/' }
	maven { url = 'https://repo.essentialsx.net/releases/' }
	maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
	maven { url = 'https://repo.rosewooddev.io/repository/public/' }
	maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
}

ext.libsDir = project.projectDir.path + '/libs/'

dependencies {
	implementation project(':api')
	
	compileOnly 'org.spongepowered:spongecommon:1.12.2-7.4.7:dev@jar'
	
	compileOnly files(libsDir + 'CombatLogX-11.5.0.0.1242.jar')
	compileOnly files(libsDir + 'GadgetsMenu-4.15.3.jar')
	compileOnly files(libsDir + 'Matrix-6.8.19.jar')
	compileOnly files(libsDir + 'MVdWPlaceholderAPI-3.1.1.jar')
	compileOnly files(libsDir + 'Negativity-2.7.1.jar')
	compileOnly files(libsDir + 'UltraCosmetics-3.8.jar')
	compileOnly files(libsDir + 'Vulcan-2.9.0.jar')
	
	compileOnly 'net.kyori:adventure-platform-bukkit:4.3.0'
	compileOnly 'net.kyori:adventure-platform-spongeapi:4.3.0'
	compileOnly 'com.github.cliftonlabs:json-simple:4.0.1'
	compileOnly 'com.viaversion:viaversion:5.0.0-SNAPSHOT'
	compileOnly 'com.viaversion:viaversion-bukkit:5.0.0-SNAPSHOT'
	compileOnly 'com.viaversion:viaversion-sponge:5.0.0-SNAPSHOT'
	compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.0'
	compileOnly 'net.essentialsx:EssentialsX:2.19.0'
	compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
	compileOnly 'net.luckperms:api:5.4'
	compileOnly 'me.clip:placeholderapi:2.11.3'
	compileOnly 'dev.esophose:playerparticles:8.0'
	compileOnly 'com.discordsrv:discordsrv:1.27.0'
}

task assembleFinalJar(type: Jar) {
	dependsOn shadowJar
	dependsOn processResources
	
	archiveFileName = 'ChatPlugin-FREE-' + rootProject.version + '.jar'
	
	from sourceSets.main.output
	
	exclude 'me/remigio07/chatplugin/common/**'
	exclude 'me/remigio07/chatplugin/server/**'
	exclude 'me/remigio07/chatplugin/ChatPluginImpl.class'
	exclude 'me/remigio07/chatplugin/ChatPluginImpl$1.class'
	
	manifest {
		attributes(["paperweight-mappings-namespace": "mojang"])
	}
	
	from project(':api').sourceSets.main.output
	from shadowJar.archiveFile
}

jar {
	archiveFileName = 'ChatPlugin-ORIGINAL.jar'
	from sourceSets.main.output
	finalizedBy assembleFinalJar
}

shadowJar {
	archiveFileName = 'ChatPlugin.jar'
	
	dependencies {
		include(dependency('me.remigio07.chatplugin:.*'))
	}
	
	include '**/*.class'
	
	relocate 'com.github.cliftonlabs.json_simple', 'me.remigio07.chatplugin.common.lib.com.github.cliftonlabs.json_simple'
	relocate 'net.kyori.adventure', 'me.remigio07.chatplugin.common.lib.net.kyori.adventure'
}

processResources {
	filesMatching(['plugin.yml', 'mcmod.info', 'bungee.yml']) {
		expand 'version': rootProject.version
	}
}
